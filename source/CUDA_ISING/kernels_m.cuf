MODULE kernels_m
   USE precision_m
   USE curand_device
   USE cudafor
   IMPLICIT NONE

CONTAINS

   ATTRIBUTES(GLOBAL) SUBROUTINE geometry(af, bf, dim)

   IMPLICIT NONE
   INTEGER, VALUE :: dim
   INTEGER, DIMENSION(dim), DEVICE :: af, bf
   INTEGER :: idx

   idx = threadIdx%x

   IF (idx .le. (dim - 1)) THEN
      af(idx) = idx + 1
      bf(idx + 1) = idx
   ELSE
      IF (idx .eq. dim) THEN
         af(idx) = 1
         bf(1) = idx
      END IF
   END IF

END SUBROUTINE geometry

ATTRIBUTES(GLOBAL) SUBROUTINE initialize_field(fld, dim, str)

IMPLICIT NONE
INTEGER, VALUE :: dim, str
INTEGER, DIMENSION(dim, dim), DEVICE :: fld
TYPE(curandStateXORWOW) :: h
INTEGER(KIND = 8) :: seed, seq, offset
INTEGER :: row, col, idx, i, j

row = 2 * blockIdx%x
col = 2 * threadIdx%x
idx = blockDim%x * (blockIdx%x - 1) + threadIdx%x
seed = idx * 64 + 1234
seq = 0
offset = 0

IF ((row .le. dim) .and. (col .le. dim)) THEN
   IF (str .eq. 0) THEN
      DO i = 0, 1
         DO j = 0, 1
            fld(row - i, col - j) = 1
         END DO
      END DO
   ELSE
      CALL curand_init(seed, seq, offset, h)
      DO i = 0, 1
         DO  j = 0, 1
            fld(row - i, col - j) = SIGN(1.0_fp_kind, 1.0_fp_kind - 2.0_fp_kind * curand_uniform_double(h))
         END DO
      END DO
   END IF
END IF

END SUBROUTINE initialize_field

ATTRIBUTES(GLOBAL) SUBROUTINE energy_partial(fld, dim, af, bf, h,  en_p_array)

IMPLICIT NONE
INTEGER, VALUE :: dim
INTEGER :: i, j, row, col
INTEGER, DIMENSION(dim, dim), DEVICE :: fld
INTEGER, DIMENSION(dim), DEVICE :: af, bf
REAL(KIND = fp_kind), DIMENSION(dim / 2), DEVICE :: en_p_array
REAL(KIND = fp_kind), VALUE :: h
REAL(KIND = fp_kind), SHARED :: psum_array(*)
INTEGER :: force, r, c
REAL(KIND = fp_kind) :: thread_sum, block_sum

row = 2 * blockIdx%x
col = 2 * threadIdx%x

thread_sum = 0.0_fp_kind

DO i = 0, 1
   DO j = 0, 1
      r = row - i
      c = col - j
      force = fld(r, bf(c)) + fld(r, af(c)) + fld(bf(r), c) + fld(af(r), c)
      thread_sum = thread_sum - REAL(fld(r, c), fp_kind) * (0.5_fp_kind * REAL(force, fp_kind) + h)
   END DO
END DO
CALL syncthreads()

psum_array(threadIdx%x) = thread_sum

CALL syncthreads()

IF (threadIdx%x .eq. 1) THEN
   block_sum = 0.0_fp_kind
   DO i = 1, (dim / 2)
      block_sum = block_sum + psum_array(i)
   END DO
   en_p_array(blockIdx%x) = block_sum
END IF

END SUBROUTINE energy_partial

ATTRIBUTES(GRID_GLOBAL) SUBROUTINE MC_update(fld, dim, af, bf, h, b, i_m, n_d)

USE cooperative_groups
IMPLICIT NONE
INTEGER, VALUE :: dim, n_d, i_m
TYPE(grid_group) :: gg
TYPE(curandStateXORWOW) :: h_r
INTEGER(KIND = 8) :: seed, seq, offset
INTEGER, DIMENSION(dim, dim), DEVICE :: fld
INTEGER, DIMENSION(dim), DEVICE :: af, bf
REAL(KIND = fp_kind), VALUE :: h, b
REAL(KIND = fp_kind) :: expo, rnd, rate
INTEGER :: i, i_row, i_d, idx, col, row, r, c, force, spin

gg = this_grid()
col = 2 * threadIdx%x
idx = (blockIdx%x - 1) * blockDim%x + threadIdx%x

seed = idx * (i_m ** 2) * 64 + 12345
seq = 0
offset = 0

CALL curand_init(seed, seq, offset, h_r)

DO i_d = 1, n_d
   DO i_row = blockIdx%x, (dim / 2), gridDim%x
      DO i = 0, 1
         row = 2 * i_row
         r = row - i
         c = col - i
         spin = fld(r, c)
         force = fld(r, bf(c)) + fld(r, af(c)) + fld(af(r), c) + fld(bf(r), c)
         expo = - 2.0_fp_kind * REAL(spin, fp_kind) * b * (REAL(force, fp_kind) + h)
         IF (expo .ge. 0.0_fp_kind) THEN
            fld(r, c) = - spin
         ELSE
            rate = exp(expo)
            rnd = curand_uniform_double(h_r)
            IF (rnd .le. rate) THEN
               fld(r, c) = - spin
            END IF
         END IF
      END DO
   END DO

   CALL SYNCTHREADS(gg)

   DO i_row = blockIdx%x, (dim / 2), gridDim%x
      DO i = 0, 1
         row = 2 * i_row
         r = row - 1 + i
         c = col - i
         spin = fld(r, c)
         force = fld(r, bf(c)) + fld(r, af(c)) + fld(af(r), c) + fld(bf(r), c)
         expo = - 2.0_fp_kind * REAL(spin, fp_kind) * b * (REAL(force, fp_kind) + h)
         IF (expo .ge. 0.0_fp_kind) THEN
            fld(r, c) = - spin
         ELSE
            rate = exp(expo)
            rnd = curand_uniform_double(h_r)
            IF (rnd .le. rate) THEN
               fld(r, c) = - spin
            END IF
         END IF
      END DO
   END DO

   CALL SYNCTHREADS(gg)

END DO

END SUBROUTINE MC_update

ATTRIBUTES(HOST) SUBROUTINE Update_Metropolis(fld, dim, af, bf, h, b, i_mis, n_dec)

IMPLICIT NONE
INTEGER, VALUE :: dim, n_dec, i_mis
INTEGER, DIMENSION(dim, dim), DEVICE :: fld
INTEGER, DIMENSION(dim), DEVICE :: af, bf
REAL(KIND = fp_kind), VALUE :: h, b
INTEGER :: istat, tB, bG

istat = CUDAOCCUPANCYMAXACTIVEBLOCKSPERMULTIPROCESSOR(tB, MC_update, dim / 2, 0)
CALL MC_update<<<tB, dim / 2>>>(fld, dim, af, bf, h, b, i_mis, n_dec)

END SUBROUTINE Update_Metropolis


ATTRIBUTES(HOST) FUNCTION Energy(fld, dim, af, bf, h)

IMPLICIT NONE
INTEGER, VALUE :: dim
INTEGER, DIMENSION(dim, dim), DEVICE :: fld
INTEGER, DIMENSION(dim), DEVICE :: af, bf
REAL(KIND = fp_kind), DIMENSION(dim / 2), DEVICE  :: en_array
REAL(KIND = fp_kind), VALUE :: h
REAL(KIND = fp_kind) :: Energy
TYPE(dim3) :: tBlock, Grid

tBlock = dim3(dim / 2, 1, 1)
Grid = dim3(dim / 2, 1, 1)

CALL energy_partial<<<Grid, tBlock, (dim / 2) * fp_kind>>>(fld, dim, af, bf, h, en_array)

Energy = SUM(en_array) / REAL(dim ** 2, fp_kind)

END FUNCTION Energy

ATTRIBUTES(HOST) FUNCTION Magnetization(fld, dim)

IMPLICIT NONE
INTEGER :: dim
INTEGER, DIMENSION(dim, dim), DEVICE :: fld
REAL(KIND = fp_kind) :: Magnetization

Magnetization = REAL(SUM(fld), fp_kind) / REAL(dim ** 2, KIND = fp_kind)

END FUNCTION Magnetization

END MODULE kernels_m

